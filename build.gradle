/******************************************************************************
* This file is part of euler-kt                                               *
* Copyright (C) 2023 Bill Kozak                                               *
*                                                                             *
* This program is free software: you can redistribute it and/or modify        *
* it under the terms of the GNU Lesser General Public License as published by *
* the Free Software Foundation, either version 3 of the License, or           *
* (at your option) any later version.                                         *
*                                                                             *
* This program is distributed in the hope that it will be useful,             *
* but WITHOUT ANY WARRANTY; without even the implied warranty of              *
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the               *
* GNU Lesser General Public License for more details.                         *
*                                                                             *
* You should have received a copy of the GNU Lesser General Public License    *
* along with this program.  If not, see <https://www.gnu.org/licenses/>.      *
******************************************************************************/

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'application'
    id "org.jetbrains.kotlin.kapt" version "1.8.10"
}

ext.mainClass = 'euler_kt.main.MainKt'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'info.picocli:picocli:4.7.+'
    implementation 'org.openjdk.jmh:jmh-core:1.33'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    kapt 'org.openjdk.jmh:jmh-generator-annprocess:1.33'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

application {
    mainClass = project.mainClass
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('build') {
    dependsOn('generateScripts')
}

tasks.register('generateScripts') {
    dependsOn('jar')
    doLast {
        new File('scripts').mkdirs()
        def script = new File('scripts/euler-kt')

        def jarPath = jar.archiveFile.get().asFile
        def classPath = sourceSets.main.compileClasspath.asPath

        script.text = """#!/usr/bin/env bash

            exec java -cp ${classPath}:${jarPath} ${project.mainClass} "\${@}"
        """.stripIndent()
        script.setExecutable(true)
    }
}
